(* Parser for block content *)
(* TODO should make more use of instaparse compositionality *)
(* TODO I suppose there should be separate versions of this for Roam and Logseq. *)

block = block-property* (heading | hiccup | blockquote | prop-block | (syntax-in-block / text)*) block-property*

<syntax-in-block> = (todo | done | image | alias | block-embed | hashtag | page-link | block-ref | code-line | code-block | video | bold | italic | bare-url | page-alias | hr | latex | tweet ) (* add query later*)

(* page links do not want their innards parsed *)
page-link = #"\[\[.*?\]\]" 
alias = #"\[([^\[\]]+?)\]\(.+?\)"  (* Note this hairy but correct char class that means everything but square brackets *)
image = #"!\[(.*?)\]\(.*?\)(\{.*\})?"   (* image = "!" alias doesn't quite work *)
hashtag = #"\#[\w-:]+" | #"#\[\[.*?\]\]"
block-ref = #"\s*\(\(([\w\s\d-]+)\)\)"
(* metadata-tag = #"^.+?::" *) (* Roam *)
block-property =  #"(\n)?.+?:: .+(\n)?"
code-line = #"\`.*?\`"
code-block = #"(?is)^```.*```"
(* query = #"\{\{query: .*?:.*?\}\}" | #"\{\{[[query]]: .*?:.*?\}\}" *)
page-alias = #"\{\{alias\:\[\[.+\]\].*\}\}"

bold = <"**">, (!bold syntax-in-block / textier)* , <"**">
roam-italic = <"__">, (!italic syntax-in-block / textier)+ , <"__">
logseq-italic = <"_">, (!italic syntax-in-block / textier)+ , <"_">
logseq-italic2 = <"*">, (!italic syntax-in-block / textier)+ , <"*">
italic = roam-italic | logseq-italic | logseq-italic2

(* Don't use these so turning off for performance
 strikethrough = "~~", (syntax-in-block / text)* , "~~"
 highlight = "^^", (syntax-in-block / text)* , "^^"  *)

todo = "{{[[TODO]]}}"
done = "{{[[DONE]]}}"

block-embed = #"\{\{embed: .*?\}\}" | #"\{\{\[\[embed\]\]: .*?\}\}"
bare-url =  #"https?:\/\/(www\.)?[-a-zA-Z0-9@:%._\+~#=]{1,256}\.[a-zA-Z0-9()]{1,6}\b([-a-zA-Z0-9()@:%_\+.~#?&//=]*)"

blockquote = <"> "> block
hr = #"^\s*---"   (* spaces are for logseq which insists on adding them spuriously  *)
latex= #"\$\$(.*?)\$\$"

(* should match all punc that is not otherwise used for syntax, not sure I have everything  *)
   text = #'[A-Za-z0-9\.\,\!\"\'\?]+|[\s\W_]+?'
textier = #'[A-Za-z0-9\.\,\!\"\'\?\s\:\-â€™]+' 

heading = #"#{1,3}" block

prop-block = #"---\n(.|\n)*---\n\n"

(* TODO maybe a general {{ }} rule *)
tweet = <"{{tweet ">, bare-url, <"}}">
video = ( <"{{video"> |  <"{{youtube">), #"\s+", bare-url, <"}}">  
zotero = <"{{zotero-imported-file ">, #"\s+" , <"}}">

hiccup = #"(?s)\[\:[a-z]+.*"
